plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

apply from: '../dependencies.gradle'

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        vectorDrawables.useSupportLibrary = true

        applicationId "com.mcgrady.xproject.retromusic"
        versionCode 10592
        versionName '6.0.1-beta'

        buildConfigField("String", "GOOGLE_PLAY_LICENSING_KEY", "\"${getProperty(getProperties('../public.properties'), 'GOOGLE_PLAY_LICENSE_KEY')}\"")
    }
    signingConfigs {
        release {
            Properties properties = getProperties('retro.properties')
            storeFile file(getProperty(properties, 'storeFile'))
            keyAlias getProperty(properties, 'keyAlias')
            storePassword getProperty(properties, 'storePassword')
            keyPassword getProperty(properties, 'keyPassword')
        }
    }
    buildTypes {
        release {
            versionNameSuffix "_" + getDate()
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'
        }
    }

    buildFeatures{
        viewBinding true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE', 'META-INF/java.properties']
        }
    }
    lint {
        warning 'MissingTranslation', 'ImpliedQuantity', 'Instantiatable'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = rootProject.file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getProperty(Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing"
}

static def getDate() {
    new Date().format('MMddyyyyss')
}

dependencies {
    implementation project(':common-theme')
    implementation "androidx.gridlayout:gridlayout:${versions.gridlayout}"

    implementation "androidx.appcompat:appcompat:${versions.appompat}"
    implementation "androidx.annotation:annotation:${versions.annotation}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerView}"
    implementation "androidx.preference:preference-ktx:${versions.preference}"
    implementation "androidx.core:core-ktx:${versions.coreKtx}"
    implementation "androidx.palette:palette-ktx:${versions.palette}"

    //Cast Dependencies
    implementation 'androidx.mediarouter:mediarouter:1.3.0'
    implementation 'com.google.android.gms:play-services-cast-framework:21.0.1'
    //WebServer by NanoHttpd
    implementation "org.nanohttpd:nanohttpd:2.3.1"

    implementation "androidx.navigation:navigation-runtime-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"

    implementation "androidx.room:room-runtime:${versions.room}"
    implementation "androidx.room:room-ktx:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${versions.lifecycle}"

    implementation "androidx.core:core-splashscreen:${versions.splashscreen}"

    implementation "com.google.android.play:feature-delivery:${versions.googlePlay}"
    implementation "com.google.android.play:review:${versions.googlePlay}"

    implementation "com.google.android.material:material:${versions.material}"

    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"

    implementation "com.afollestad.material-dialogs:core:${versions.materialDialogs}"
    implementation "com.afollestad.material-dialogs:input:${versions.materialDialogs}"
    implementation "com.afollestad.material-dialogs:color:${versions.materialDialogs}"

    implementation 'com.afollestad:material-cab:2.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"

    implementation "io.insert-koin:koin-core:${versions.koin}"
    implementation "io.insert-koin:koin-android:${versions.koin}"

    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    kapt "com.github.bumptech.glide:compiler:${versions.glide}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${versions.glide}"

    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'

    implementation 'com.github.bosphere.android-fadingedgelayout:fadingedgelayout:1.0.0'

    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC3'
    implementation 'com.github.jetradarmobile:android-snowfall:1.2.1'

    implementation "dev.chrisbanes.insetter:insetter:0.6.1"

    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'
    implementation 'com.github.Adonai:jaudiotagger:2.3.15'
    implementation 'com.anjlab.android.iab.v3:library:2.0.3'
    implementation 'com.r0adkll:slidableactivity:2.1.0'
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    implementation 'com.github.dhaval2404:imagepicker:2.1'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.8'
    implementation 'cat.ereza:customactivityoncrash:2.4.0'
    implementation 'me.tankery.lib:circularSeekBar:1.4.0'
}
